plugins {
  id 'java'
  id 'org.springframework.boot'
}

apply from: jacocoBuildScript

dependencies {
  implementation project(apiModule)
  implementation project(persistenceModule)
  implementation project(serviceModule)
}

def disable = { enabled = false }
def enable = { enabled = true }

allprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'io.spring.dependency-management'

  group = groupId
  version = projectVersion

  repositories {
    mavenCentral()
    mavenLocal()
  }

  java {
    toolchain {
      languageVersion.set(JavaLanguageVersion.of(javaVersion))
    }
  }

  bootJar disable
  jar enable
}

subprojects {
  apply plugin: 'org.springframework.boot'

  dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation platform('org.junit:junit-bom')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.awaitility:awaitility'
  }

  bootJar disable
  jar enable

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
  }
}
